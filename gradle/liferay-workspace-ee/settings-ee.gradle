//
// This file is supposed to be located in 'gradle/liferay-workspace-ee' and
// loaded from and being included by 'settings.gradle' (using '(apply from:...)')
//

// Liferay Workspace EE will use 'project.name' as package name for DEB / RPM
// archives and also to name the built files.
//
// It's strongly recommended to name your project explicitly (Gradle uses rootProject's
// directory name as default, which might not be accurate).


//rootProject.name = 'liferay-project-abc'

//
// Load (include) the extra sub-projects adding the EE features
//

// Based on 'https://github.com/gradle/gradle/blob/master/settings.gradle'
include 'liferay-workspace-ee'
include 'liferay-workspace-ee:aws'
include 'liferay-workspace-ee:jenkins'
include 'liferay-workspace-ee:ospackage'
include 'liferay-workspace-ee:docker'

def rootEeProject = project(":liferay-workspace-ee")
assert rootEeProject.parent == rootProject

(rootEeProject.children + rootEeProject).each { eeProject ->

    logger.info "[LWE] Configuring EE subproject '${eeProject.path}'"

    // relative to root of the Gradle project
    String eeProjectDirPath = "gradle/${eeProject.path.replace(":", "/")}"
    eeProject.projectDir = new File(settingsDir, eeProjectDirPath)
    eeProject.buildFileName = "${eeProject.name}.gradle"

    assert eeProject.projectDir.isDirectory()
    assert eeProject.buildFile.isFile()
    assert eeProject.parent == rootEeProject || (eeProject == rootEeProject && eeProject.parent == rootProject)
}


// enforce JDK 8+ to be used, since Liferay 7.0+ bundle has be run with Oracle JDK 8+
//
// check is based on: http://stackoverflow.com/a/28065582/4966203

if (!JavaVersion.current().isJava8Compatible()) {
    throw new IllegalStateException(
            "ERROR: Java 1.8+ is required but ${JavaVersion.current()} found. Please change your " +
            "JAVA_HOME environment variable / java command in PATH to point to JDK 8+. " +
            "Liferay 7.0 is JDK8+ only, so you should use this version to compile all the sources.");
}


// enforce Gradle version to be 2.14+ or 3.0+, since we use some newer syntax and features

def gradleVersion = org.gradle.util.GradleVersion.current().baseVersion.version

if (gradleVersion.with { startsWith('0.') || startsWith('1.') || (startsWith('2.') && !startsWith('2.14')) }) {
    throw new IllegalStateException(
            "ERROR: Gradle 2.14+ or 3+ is required for liferay-workspace-ee, but '${gradleVersion.version}' " +
                    "is being used. Please change your Gradle Wrapper config " +
                    "(/gradle/wrapper/gradle-wrapper.properties) or use 'gradle' binary " +
                    "which is from supported Gradle installation.");
}